      /TITLE Copy Data Description Specifications Fields
     F*CMDBFR OVRDBF CPYFSRC QDDSSRC
     F*CMDBFR OVRDBF CPYTSRC QDDSSRC
     F*PARMS
      *
     H* SYNOPSIS : Copy fields from specified DDS member.
      *
     H* Generated by  :
     H* Function type :
      *
     H* Company       : Businessland, Inc.
     H* System        : FasTRACS
     H* User name     : Tim Hawkins
     H* Date          : 10/11/90  Time  : 15:00:00
     H* Copyright     : Businessland FasTRACS
      *
      *================================================================
     F* Maintenance   :
      *================================================================
      /EJECT
      *
      *
     FQDDSSRC   IF   E             DISK
     F                                     RENAME(QDDSSRC:QDDSSRCR)
     FCPYFSRC   IF   E             DISK    USROPN
     F                                     RENAME(QDDSSRC:CPYFSRCR)
     FCPYTSRC   O  A E             DISK
     F                                     RENAME(QDDSSRC:CPYTSRCR)
      *
     D FLD             S             10    DIM(1000)                            FIELDS PROCESSED
     D AY1             S              1    DIM(80)                              WORK AREA, 1
      *
     D TXT             S             80    DIM(12) CTDATA PERRCD(1)             TEXT
      *
     D PGMDS         ESDS                  EXTNAME(DSPGM)
      *
     IQDDSSRCR
      *
     ICPYFSRCR
     I              SRCSEQ                      RNMSEQ
      *
     C     CPYMBR        IFEQ      *BLANKS
     C                   EXSR      $SCANC                                       SCAN FOR /COPY
     C                   RETURN
     C                   ELSE
      *
     C                   EXSR      $FNDRC                                       FIND RECORD
     C                   EXSR      $LODTS                                       TIME STAMP.
     C                   EXSR      $CPYTS                                       COPY TARGET
     C                   EXSR      $CPYKY                                       COPY KEYS/SEL
     C                   MOVE      ON            *INLR
     C                   END
      /SPACE 3
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *  SCAN SOURCE MEMBER STATEMENT FOR /COPY.
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
     CSR   $SCANC        BEGSR
      *
     C     *IN99         DOUEQ     ON                                           FIND /COPY
     C     RESULT        ORGT      *ZERO                                         IN ORIGINAL
     C                   READ      QDDSSRCR                               99     SOURCE FILE.
     C     *IN99         IFEQ      OFF
     C                   WRITE     CPYTSRCR
     C     '/COPY'       SCAN      SRCDTA        RESULT                         String
     C                   END
     C                   END
      *
     C     RESULT        IFLE      *ZERO                                        NOT FOUND.
     C                   MOVE      OFF           FOUND
     C                   MOVE      ON            *INLR
     C                   RETURN
     C                   ELSE
     C                   MOVE      ON            FOUND
     C                   END
      *
     C     RESULT        ADD       6             X                               TARGET
     C                   MOVEA     SRCDTA        AY1                              MEMBER
     C                   MOVEA     AY1(X)        CPYMBR                           NAME.
      *
     C     ','           SCAN      SRCDTA        RESULT
      *
     C     RESULT        IFGT      *ZERO
     C     RESULT        ANDLT     28                                           LOAD NEW
     C     RESULT        ADD       1             X                               FIELD PREFIX.
     C                   MOVEA     SRCDTA        AY1
     C                   MOVEA     AY1(X)        NEWPFX
     C                   MOVE      RESULT        X                              FIX MEMBER
     C                   MOVEA     '        '    AY1(X)                          NAME.
     C                   MOVEA     AY1(14)       CPYMBR
     C                   END
      *
     CSR   #SCANC        ENDSR
      /SPACE 3
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *  FIND RECORD IN TARGET SOURCE MEMBER.
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
     CSR   $FNDRC        BEGSR
      *
     C                   OPEN      CPYFSRC
     C                   MOVE      *BLANKS       WK012A
     C                   MOVEL     'A'           WK012A
     C                   MOVE      'R'           WK012A
      *
     C     *IN99         DOUEQ     ON                                           FIND RECORD
     C     RESULT        ORGT      *ZERO                                         NAME IN
     C                   READ      CPYFSRCR                               99     TARGET SOURCE
     C     *IN99         IFEQ      OFF                                           FILE.
     C     WK012A        SCAN      SRCDTA        RESULT                         String
     C     result        ifne      *zero                                         FILE.
     C                   eval      result2 = %scan('+':srcdta)                  String
     C     result2       ifne      *zero                                         FILE.
     C                   READ      CPYFSRCR                               99     TARGET SOURCE
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C     *IN99         IFEQ      ON                                           RECORD NAME
     C                   MOVE      OFF           FOUND                           NOT FOUND;
     C                   MOVE      ON            *INLR
     C                   RETURN                                                  IGNORE.
     C                   END
      *
     CSR   #FNDRC        ENDSR
      /SPACE 3
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *  LOAD TIME STAMP TO WORK MEMBER.
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
     CSR   $LODTS        BEGSR
      *
     C                   MOVE      *ZERO         SRCDAT
      *
     C                   MOVEA     TXT(1)        SRCDTA                         BLANK LINE
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(2)        AY1                            JOB
     C                   MOVEA     ZZJOB         AY1(38)
     C                   MOVEA     AY1           SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(3)        AY1                            USER
     C                   MOVEA     ZZUSR         AY1(38)
     C                   MOVEA     AY1           SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(4)        AY1                            NUMBER
     C                   MOVE      ZZJNO         WK006A
     C                   MOVEA     WK006A        AY1(38)
     C                   MOVEA     AY1           SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(5)        SRCDTA                         BLANK LINE
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(6)        AY1                            FILE
     C                   MOVEA     FILE          AY1(38)
     C                   MOVEA     AY1           SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(7)        AY1                            LIBRARY
     C                   MOVEA     LIB           AY1(38)
     C                   MOVEA     AY1           SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(8)        AY1                            MEMBER
     C                   MOVEA     CPYMBR        AY1(38)
     C                   MOVEA     AY1           SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(9)        SRCDTA                         BLANK LINE
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   eval      srcdta = TXT(10)                             DATE
     C                   eval      %subst(srcdta:38) = %editc(udate:'Y')        DATE
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   eval      srcdta = TXT(11)                             DATE
     C                   eval      %subst(srcdta:38) =                          DATE
     C                               %editw(utime:'  .  .  ')                   DATE
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     C                   MOVEA     TXT(12)       SRCDTA                         BLANK LINE
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
      *
     CSR   #LODTS        ENDSR
      /SPACE 3
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *  COPY TARGET SOURCE MEMBER.
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
     CSR   $CPYTS        BEGSR
      *
     C     *IN99         DOUEQ     ON
     C     WK001A        OREQ      'K'
     C     WK001A        OREQ      'S'
     C                   READ      CPYFSRCR                               99
     C     *IN99         IFEQ      OFF
     C                   MOVEL     SRCDTA        WK017A
     C                   MOVE      WK017A        WK001A
     C     WK001A        IFNE      'K'
     C     WK001A        ANDNE     'S'
     C     WK001A        IFEQ      *BLANKS
     C                   MOVEL     SRCDTA        WK020A
     C                   MOVE      WK020A        WK002A
     C     WK002A        IFNE      *BLANKS
     C     NEWPFX        ANDNE     *BLANKS
     C                   MOVE      NEWPFX        WK020A
     C                   MOVEL     WK020A        SRCDTA
     C                   END
     C                   END
     C                   MOVEL     '     C'      SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
     C                   END
     C                   END
     C                   END
      *
     CSR   #CPYTS        ENDSR
      /SPACE 3
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *  COPY SOURCE MEMBER STATEMENTS (KEYS/SELECT/OMIT)
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
     CSR   $CPYKY        BEGSR
      *
     C     *IN99         DOUEQ     ON
     C                   MOVE      SRCSEQ        SAVSEQ
     C                   READ      QDDSSRCR                               99
     C     *IN99         IFEQ      OFF
     C                   MOVE      SAVSEQ        SRCSEQ
     C                   MOVEL     SRCDTA        WK006A
     C                   MOVE      WK006A        WK001A
     C     WK001A        IFNE      'C'
     C                   MOVEL     SRCDTA        WK020A
     C                   MOVE      WK020A        WK002A
     C                   ADD       1             SRCSEQ
     C                   WRITE     CPYTSRCR
     C                   END
     C                   END
     C                   END
      *
     CSR   #CPYKY        ENDSR
      /SPACE 3
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *  INITIALIZE PROGRAM VARIABLES
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
     CSR   *INZSR        BEGSR
      *
     C                   MOVE      '0'           OFF               1
     C                   MOVE      '1'           ON                1
      *
     C                   MOVE      *BLANKS       NEWPFX            2
     C                   MOVE      *BLANKS       WK001A            1
     C                   MOVE      *BLANKS       WK002A            2
     C                   MOVE      *BLANKS       WK006A            6
     C                   MOVE      *BLANKS       WK012A           12
     C                   MOVE      *BLANKS       WK017A           17
     C                   MOVE      *BLANKS       WK020A           20
     C                   MOVE      *ZERO         X                 3 0
     C                   MOVE      *ZERO         RESULT            3 0
     C                   MOVE      *ZERO         RESULT2           3 0
     C                   time                    Utime             6 0
      *
     CSR   #INZSR        ENDSR
      /SPACE 3
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *  DEFINE PROGRAM VARIABLES
      * * * * * * * * * * * * * * * * * * * * * * * * * * *
     CSR   $ZDEFN        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    FILE             10
     C                   PARM                    LIB              10
     C                   PARM                    CPYMBR           10
     C                   PARM                    FOUND             1
     C                   PARM                    ERROR             1
      *
     C     *LIKE         DEFINE    SRCSEQ        SAVSEQ
      *
     CSR   #ZDEFN        ENDSR
      *
**    * TXT  - TEXT FOR WORK MEMBER
     C*
     C*     Source copy job  . . . . xxxxxxxxxx
     C*     Source copy user . . . . xxxxxxxxxx
     C*     Source copy number . . . xxxxxx
     C*
     C*     Source copy file . . . . xxxxxxxxxx
     C*     Source copy library  . . xxxxxxxxxx
     C*     Source copy member . . . xxxxxxxxxx
     C*
     C*     Source copy date . . . . mm/dd/yy
     C*     Source copy time . . . . hh:mm:ss
     C*
